plugins {
    id 'groovy'
    id "com.jfrog.bintray" version "1.8.4"
    id "com.github.dcendents.android-maven" version "2.1"
}

def siteUrl = 'https://github.com/rayworks/AspectDroidPlugin'
def gitUrl = 'https://github.com/rayworks/AspectDroidPlugin.git'

group 'org.aspect.droid'
// artifactId plugin
version '1.0.0'

repositories {
    google()
    mavenCentral()
}

ext {
    aspectj_version = "1.8.8"
}

targetCompatibility = JavaVersion.VERSION_1_7
sourceCompatibility = JavaVersion.VERSION_1_7

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    compile gradleApi()
    compile localGroovy()
    compile 'com.android.tools.build:gradle:3.3.2'
    compile "org.aspectj:aspectjtools:$aspectj_version"
    compile "org.aspectj:aspectjrt:$aspectj_version"
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'jar'
                name 'The AOP plugin for Android'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'rayworks'
                        name 'Sean Zhou'
                        email 'crayzhou@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "AspectDroidPlugin"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}